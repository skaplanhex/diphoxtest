C*****************************************************
C FONCTION P3DI+P4DI
C*****************************************************	
C*****************************************************
C PARTIE FINIE POUR PT5 > PTM
C*****************************************************
C*****************************************************
	SUBROUTINE SSY3RT(S,GPT3,Y3,Y4,X3,X4,Z3,IH1,IH2,IH3,IH4,SY3R)
	IMPLICIT REAL*8 (A-H,L-Z)
	COMMON/COUP/PTM,R
	PARAMETER (K0MAX=84)
	DIMENSION PART35R(K0MAX),SF(K0MAX)
	DIMENSION SY3R(K0MAX),TEMP(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	YS = (Y3-Y4)/2.D0
	Y = (Y3+Y4)/2.D0
	PT3 = GPT3/X3
	X10 = PT3*(DEXP(Y3)+DEXP(Y4))/DSQRT(S)
	X20 = PT3*(DEXP(-Y3)+DEXP(-Y4))/DSQRT(S)
	X1P = X10/Z3
	X2P = X20/Z3
c attention les elements de matrice 2 -> 2 sont independant de 
c l'echelle d'energie
	SC = X10*X20*S
	TC = -X10*DSQRT(S)*PT3*DEXP(-Y3)
	UC = -X20*DSQRT(S)*PT3*DEXP(Y3)
	CALL STRFRAT(X1P,IH1,X2P,IH2,X3,IH3,X4,IH4,SF)
	CALL SPART35RT(SC,TC,UC,Z3,R,PART35R)
	CALL VEC_DMULT_VECTOR(PART35R,SF,K0MAX,TEMP)
	TYU = 1.D0/(X3*Z3)
	CALL VEC_DMULT_CONSTANT(TEMP,K0MAX,TYU,SY3R)
	RETURN
	END
C**********************************************************************
	SUBROUTINE SSY4RT(S,GPT4,Y3,Y4,X3,X4,Z4,IH1,IH2,IH3,IH4,SY4R)
	IMPLICIT REAL*8 (A-H,L-Z)
	COMMON/COUP/PTM,R
	PARAMETER (K0MAX=84)
	DIMENSION PART45R(K0MAX),SF(K0MAX)
	DIMENSION SY4R(K0MAX),TEMP(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	YS = (Y3-Y4)/2.D0
	Y = (Y3+Y4)/2.D0
	PT4 = GPT4/X4
	X10 = PT4*(DEXP(Y3)+DEXP(Y4))/DSQRT(S)
	X20 = PT4*(DEXP(-Y3)+DEXP(-Y4))/DSQRT(S)
	X1S = X10/Z4
	X2S = X20/Z4
c attention les elements de matrice 2 -> 2 sont independant de 
c l'echelle d'energie
	SC = X10*X20*S
	TC = -X20*DSQRT(S)*PT4*DEXP(Y4)
	UC = -X10*DSQRT(S)*PT4*DEXP(-Y4)
	CALL STRFRAT(X1S,IH1,X2S,IH2,X3,IH3,X4,IH4,SF)
	CALL SPART45RT(SC,TC,UC,Z4,R,PART45R)
	CALL VEC_DMULT_VECTOR(SF,PART45R,K0MAX,TEMP)
	TYU = 1.D0/(X4*Z4)
	CALL VEC_DMULT_CONSTANT(TEMP,K0MAX,TYU,SY4R)
	RETURN
	END
C*****************************************************
C FONCTIONS POUR P3DI+P4DI	
C*****************************************************	
	SUBROUTINE SSV12T(S,GPT3,Y3,Y4,M,MU,X3,X4,X10,X20,
     #	IH1,IH2,IH3,IH4,SV12)
C INTEGRALE A 1 DIMENSIONS
	IMPLICIT REAL*8 (A-H,L-Z)
	COMMON/COUP/PTMM,R
	PARAMETER (K0MAX=84)
	DIMENSION PART15Z(K0MAX),PART15L(K0MAX),PART15D(K0MAX)
	DIMENSION PART25Z(K0MAX),PART25L(K0MAX),PART25D(K0MAX)
	DIMENSION PARTVI(K0MAX),SF(K0MAX)
	DIMENSION TEMP0(K0MAX),TEMP1(K0MAX),TEMP2(K0MAX),TEMP3(K0MAX)
	DIMENSION AU1(K0MAX),AU2(K0MAX),AU3(K0MAX),SV12(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	YS = (Y3-Y4)/2.D0
	Y = (Y3+Y4)/2.D0
	PT3 = GPT3/X3
	PT4 = PT3
	PT5X = DSQRT(S)/(2.D0*DCOSH(YS))*(1.D0-X10)*(1.D0-X20)/
     #  ((1.D0-X10)*DEXP(-Y)+(1.D0-X20)*DEXP(Y))
	PTM = DMIN1(PTMM,PT5X)
cc	PTM = PTMM
	SC = X10*X20*S
	TC = -X10*DSQRT(S)*PT3*DEXP(-Y3)
	UC = -X20*DSQRT(S)*PT3*DEXP(Y3)
	CALL STRFRAT(X10,IH1,X20,IH2,X3,IH3,X4,IH4,SF)
	CALL SPART15ZT(SC,TC,UC,UN,M,PTM,PART15Z)
	CALL SPART15LT(SC,TC,UC,UN,PART15L)
	CALL SPART15DT(SC,TC,UC,M,PTM,PART15D)
	CALL SPART25ZT(SC,TC,UC,UN,M,PTM,PART25Z)
	CALL SPART25LT(SC,TC,UC,UN,PART25L)
	CALL SPART25DT(SC,TC,UC,M,PTM,PART25D)
	CALL SPARTVIT(S,SC,TC,UC,MU,PT3,PTM,PARTVI)
C
	CALL VEC_DMULT_VECTOR(PART15Z,SF,K0MAX,TEMP0)
	CALL VEC_DMULT_VECTOR(PART15L,SF,K0MAX,TEMP1)
	CALL VEC_DMULT_VECTOR(PART15D,SF,K0MAX,TEMP2)
	TY1 = DLOG(1-X10)
	TY2 = DLOG(1-X10)**2/2.D0
	CALL VEC_DMULT_ADD(TEMP2,TEMP1,K0MAX,TY2,TEMP3)
	CALL VEC_DMULT_ADD(TEMP3,TEMP0,K0MAX,TY1,AU1)
C
	CALL VEC_DMULT_VECTOR(PART25Z,SF,K0MAX,TEMP0)
	CALL VEC_DMULT_VECTOR(PART25L,SF,K0MAX,TEMP1)
	CALL VEC_DMULT_VECTOR(PART25D,SF,K0MAX,TEMP2)
	TY1 = DLOG(1-X20)
	TY2 = DLOG(1-X20)**2/2.D0
	CALL VEC_DMULT_ADD(TEMP2,TEMP1,K0MAX,TY2,TEMP3)
	CALL VEC_DMULT_ADD(TEMP3,TEMP0,K0MAX,TY1,AU2)
C
	CALL VEC_DMULT_VECTOR(PARTVI,SF,K0MAX,AU3)
C
	CALL VEC_DADD_VECTOR(AU1,AU2,K0MAX,TEMP0)
	TY3 = 1.D0/X3
	CALL VEC_DADD_MULT_CONSTANT(TEMP0,AU3,K0MAX,TY3,SV12)
	RETURN
	END
C*****************************************************	
	SUBROUTINE SSV2T(S,GPT3,Y3,Y4,X3,X4,U2,IH1,IH2,IH3,IH4,SV2)
C INTEGRALE A 2 DIMENSIONS
	IMPLICIT REAL*8 (A-H,L-Z)
	PARAMETER (K0MAX=84)
	DIMENSION PARTVI2(K0MAX),SF(K0MAX)
	DIMENSION SV2(K0MAX),TEMP(K0MAX)
	PI=4.D0*DATAN(1.D0)
	ZERO = 0.D0
	UN = 1.D0
	YS = (Y3-Y4)/2.D0
	Y = (Y3+Y4)/2.D0
	FI = PI*U2
	PT3 = GPT3/X3
	PT4 = PT3
	X10 = PT3*(DEXP(Y3)+DEXP(Y4))/DSQRT(S)
	X20 = PT3*(DEXP(-Y3)+DEXP(-Y4))/DSQRT(S)
	if (x10.gt.1.d0.or.x20.gt.1.d0) then
	  CALL VEC_DINIT(SV2,K0MAX,ZERO)
	  return
	endif
	SC = X10*X20*S
	TC = -X10*DSQRT(S)*PT3*DEXP(-Y3)
	UC = -X20*DSQRT(S)*PT3*DEXP(Y3)
	CALL STRFRAT(X10,IH1,X20,IH2,X3,IH3,X4,IH4,SF)
	CALL SPARTVI2T(S,SC,TC,UC,FI,PARTVI2)
	CALL VEC_DMULT_VECTOR(PARTVI2,SF,K0MAX,TEMP)
	TY3 = PI/X3
	CALL VEC_DMULT_CONSTANT(TEMP,K0MAX,TY3,SV2)
	RETURN
	END
C*****************************************************	
	SUBROUTINE SSV3T(S,GPT3,Y3,MF,X3,X4,X10,X20,Z3_ISOL,
     #	IH1,IH2,IH3,IH4,SV3)
C INTEGRALE A 1 DIMENSIONS
	IMPLICIT REAL*8 (A-H,L-Z)
	COMMON/COUP/PTMM,R
	PARAMETER (K0MAX=84)
	DIMENSION PART35Z(K0MAX),PART35L(K0MAX),PART35D(K0MAX),SF(K0MAX)
	DIMENSION SV3(K0MAX),AU3(K0MAX)
	DIMENSION TEMP0(K0MAX),TEMP1(K0MAX),TEMP2(K0MAX),TEMP3(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	PT3 = GPT3/X3
	PT4 = PT3
c 
	X3MIN = 2.D0*GPT3*DCOSH(Y3)/DSQRT(S)
	Z3MIN = DMAX1(X3MIN/X3,X10,X20,Z3_ISOL)	
	SC = X10*X20*S
	TC = -X10*DSQRT(S)*PT3*DEXP(-Y3)
	UC = -X20*DSQRT(S)*PT3*DEXP(Y3)
	CALL STRFRAT(X10,IH1,X20,IH2,X3,IH3,X4,IH4,SF)
	CALL SPART35ZT(SC,TC,UC,UN,MF,PT3,PART35Z)
	CALL SPART35LT(SC,TC,UC,UN,PART35L)
	CALL SPART35DT(SC,TC,UC,MF,PT3,PART35D)
	CALL VEC_DMULT_VECTOR(PART35Z,SF,K0MAX,TEMP0)
	CALL VEC_DMULT_VECTOR(PART35L,SF,K0MAX,TEMP1)
	CALL VEC_DMULT_VECTOR(PART35D,SF,K0MAX,TEMP2)
	TY1 = DLOG(1.D0-Z3MIN)
	TY2 = DLOG(1.D0-Z3MIN)**2/2.D0
	CALL VEC_DMULT_ADD(TEMP2,TEMP1,K0MAX,TY2,TEMP3)
	CALL VEC_DMULT_ADD(TEMP3,TEMP0,K0MAX,TY1,AU3)
	TY3 = 1.D0/X3
	CALL VEC_DMULT_CONSTANT(AU3,K0MAX,TY3,SV3)
	RETURN
	END
C*****************************************************	
	SUBROUTINE SSV4T(S,GPT4,Y4,MF,X3,X4,X10,X20,Z4_ISOL,
     #	IH1,IH2,IH3,IH4,SV4)
C INTEGRALE A 1 DIMENSIONS
	IMPLICIT REAL*8 (A-H,L-Z)
	COMMON/COUP/PTMM,R
	PARAMETER (K0MAX=84)
	DIMENSION PART45Z(K0MAX),PART45L(K0MAX),PART45D(K0MAX),SF(K0MAX)
	DIMENSION SV4(K0MAX),AU4(K0MAX)
	DIMENSION TEMP0(K0MAX),TEMP1(K0MAX),TEMP2(K0MAX),TEMP3(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	PT4 = GPT4/X4
	PT3 = PT4
c 
	X4MIN = 2.D0*GPT4*DCOSH(Y4)/DSQRT(S)
	Z4MIN = DMAX1(X4MIN/X4,X10,X20,Z4_ISOL)	
	SC = X10*X20*S
	TC = -X20*DSQRT(S)*PT4*DEXP(Y4)
	UC = -X10*DSQRT(S)*PT4*DEXP(-Y4)
	CALL STRFRAT(X10,IH1,X20,IH2,X3,IH3,X4,IH4,SF)
	CALL SPART45ZT(SC,TC,UC,UN,MF,PT4,PART45Z)
	CALL SPART45LT(SC,TC,UC,UN,PART45L)
	CALL SPART45DT(SC,TC,UC,MF,PT4,PART45D)
	CALL VEC_DMULT_VECTOR(PART45Z,SF,K0MAX,TEMP0)
	CALL VEC_DMULT_VECTOR(PART45L,SF,K0MAX,TEMP1)
	CALL VEC_DMULT_VECTOR(PART45D,SF,K0MAX,TEMP2)
	TY1 = DLOG(1-Z4MIN)
	TY2 = DLOG(1-Z4MIN)**2/2.D0
	CALL VEC_DMULT_ADD(TEMP2,TEMP1,K0MAX,TY2,TEMP3)
	CALL VEC_DMULT_ADD(TEMP3,TEMP0,K0MAX,TY1,AU4)
	TY4 = 1.D0/X3
	CALL VEC_DMULT_CONSTANT(AU4,K0MAX,TY4,SV4)
	RETURN
	END
C*****************************************************	
	SUBROUTINE SSY3T(S,GPT3,Y3,Y4,M,X3,X4,U2,IH1,IH2,IH3,IH4,SY3)
	IMPLICIT REAL*8 (A-H,L-Z)
	COMMON/COUP/PTMM,R
	PARAMETER (K0MAX=84)
	DIMENSION PART15Z(K0MAX),PART15L(K0MAX)
	DIMENSION PART15Z0(K0MAX),PART15L0(K0MAX)
	DIMENSION PART25Z(K0MAX),PART25L(K0MAX)
	DIMENSION PART25Z0(K0MAX),PART25L0(K0MAX)
	DIMENSION SF(K0MAX),SF0(K0MAX)
	DIMENSION SY3(K0MAX),BU1(K0MAX),BU2(K0MAX)
	DIMENSION TEMP0(K0MAX),TEMP1(K0MAX),TEMP2(K0MAX),TEMP3(K0MAX)
	DIMENSION TEMP4(K0MAX),TEMP5(K0MAX),TEMP6(K0MAX),TEMP7(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	YS = (Y3-Y4)/2.D0
	Y = (Y3+Y4)/2.D0
	PT3 = GPT3/X3
	X10 = PT3*(DEXP(Y3)+DEXP(Y4))/DSQRT(S)
	X20 = PT3*(DEXP(-Y3)+DEXP(-Y4))/DSQRT(S)
	if (x10.gt.un.or.x20.gt.un) then
	  CALL VEC_DINIT(SY3,K0MAX,ZERO)
	  return
	endif
	PT5X = DSQRT(S)/(2.D0*DCOSH(YS))*(1.D0-X10)*(1.D0-X20)/
     #  ((1.D0-X10)*DEXP(-Y)+(1.D0-X20)*DEXP(Y))
	PTM = DMIN1(PTMM,PT5X)
cc	PTM = PTMM
	SC = X10*X20*S
	TC = -X10*DSQRT(S)*PT3*DEXP(-Y3)
	UC = -X20*DSQRT(S)*PT3*DEXP(Y3)
	Z1MAX = 1.D0
	Z1MIN = X10
	Z1 = Z1MIN + (Z1MAX-Z1MIN)*U2
	CALL STRFRAT(X10/Z1,IH1,X20,IH2,X3,IH3,X4,IH4,SF)
	CALL STRFRAT(X10,IH1,X20,IH2,X3,IH3,X4,IH4,SF0)
	CALL SPART15ZT(SC,TC,UC,Z1,M,PTM,PART15Z)
	CALL SPART15ZT(SC,TC,UC,UN,M,PTM,PART15Z0)
	CALL SPART15LT(SC,TC,UC,Z1,PART15L)
	CALL SPART15LT(SC,TC,UC,UN,PART15L0)
	CALL VEC_DMULT_VECTOR(PART15Z,SF,K0MAX,TEMP0)
	CALL VEC_DMULT_VECTOR(PART15Z0,SF0,K0MAX,TEMP1)
	TY1 = 1.D0/Z1**2
	CALL VEC_DMULT_SUB(TEMP1,TEMP0,K0MAX,TY1,TEMP2)
	TY2 = 1.D0/(1.D0-Z1)
	CALL VEC_DMULT_CONSTANT(TEMP2,K0MAX,TY2,TEMP3)
C
	CALL VEC_DMULT_VECTOR(PART15L,SF,K0MAX,TEMP4)
	CALL VEC_DMULT_VECTOR(PART15L0,SF0,K0MAX,TEMP5)
	CALL VEC_DMULT_SUB(TEMP5,TEMP4,K0MAX,TY1,TEMP6)
	TY3 = DLOG(1.D0-Z1)/(1.D0-Z1)
	CALL VEC_DMULT_CONSTANT(TEMP6,K0MAX,TY3,TEMP7)
C
	TY4 = (Z1MAX-Z1MIN)/X3
	CALL VEC_DADD_MULT_CONSTANT(TEMP3,TEMP7,K0MAX,TY4,BU1)
	Z2MAX = 1.D0
	Z2MIN = X20
	Z2 = Z2MIN + (Z2MAX-Z2MIN)*U2
	CALL STRFRAT(X10,IH1,X20/Z2,IH2,X3,IH3,X4,IH4,SF)
	CALL STRFRAT(X10,IH1,X20,IH2,X3,IH3,X4,IH4,SF0)
	CALL SPART25ZT(SC,TC,UC,Z2,M,PTM,PART25Z)
	CALL SPART25ZT(SC,TC,UC,UN,M,PTM,PART25Z0)
	CALL SPART25LT(SC,TC,UC,Z2,PART25L)
	CALL SPART25LT(SC,TC,UC,UN,PART25L0)
	CALL VEC_DMULT_VECTOR(PART25Z,SF,K0MAX,TEMP0)
	CALL VEC_DMULT_VECTOR(PART25Z0,SF0,K0MAX,TEMP1)
	TY1 = 1.D0/Z2**2
	CALL VEC_DMULT_SUB(TEMP1,TEMP0,K0MAX,TY1,TEMP2)
	TY2 = 1.D0/(1.D0-Z2)
	CALL VEC_DMULT_CONSTANT(TEMP2,K0MAX,TY2,TEMP3)
C
	CALL VEC_DMULT_VECTOR(PART25L,SF,K0MAX,TEMP4)
	CALL VEC_DMULT_VECTOR(PART25L0,SF0,K0MAX,TEMP5)
	CALL VEC_DMULT_SUB(TEMP5,TEMP4,K0MAX,TY1,TEMP6)
	TY3 = DLOG(1.D0-Z2)/(1.D0-Z2)
	CALL VEC_DMULT_CONSTANT(TEMP6,K0MAX,TY3,TEMP7)
C
	TY4 = (Z2MAX-Z2MIN)/X3
	CALL VEC_DADD_MULT_CONSTANT(TEMP3,TEMP7,K0MAX,TY4,BU2)
C
	CALL VEC_DADD_VECTOR(BU1,BU2,K0MAX,SY3)
	RETURN
	END
C*****************************************************	
	SUBROUTINE SSY3PT(S,GPT3,GPT4,Y3,Y4,MF,X3,X4,Z3,IH1,
     #	IH2,IH3,IH4,SY3P)
	IMPLICIT REAL*8 (A-H,L-Z)
	COMMON/FLAGEXP/ICOUP_EXP
	COMMON/COUPEXP/PTM_EXP,R_EXP
	PARAMETER (K0MAX=84)
	DIMENSION PART35Z(K0MAX),PART35L(K0MAX)
	DIMENSION PART35Z0(K0MAX),PART35L0(K0MAX)
	DIMENSION SF(K0MAX),SF0(K0MAX)
	DIMENSION SY3P(K0MAX)
	DIMENSION TEMP0(K0MAX),TEMP1(K0MAX),TEMP2(K0MAX),TEMP3(K0MAX)
	DIMENSION TEMP4(K0MAX),TEMP5(K0MAX),TEMP6(K0MAX),TEMP7(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	YS = (Y3-Y4)/2.D0
	Y = (Y3+Y4)/2.D0
	PT3 = GPT3/X3
	X10 = PT3*(DEXP(Y3)+DEXP(Y4))/DSQRT(S)
	X20 = PT3*(DEXP(-Y3)+DEXP(-Y4))/DSQRT(S)
	X40 = GPT4/GPT3*X3
	X1P = X10/Z3
	X2P = X20/Z3
c attention les elements de matrice 2 -> 2 sont independant de 
c l'echelle d'energie
	SC = X10*X20*S
	TC = -X10*DSQRT(S)*PT3*DEXP(-Y3)
	UC = -X20*DSQRT(S)*PT3*DEXP(Y3)
	CALL STRFRAT(X1P,IH1,X2P,IH2,X3,IH3,X4,IH4,SF)
	CALL STRFRAT(X10,IH1,X20,IH2,X3,IH3,X40,IH4,SF0)
	CALL SPART35ZT(SC,TC,UC,Z3,MF,PT3,PART35Z)
	CALL SPART35ZT(SC,TC,UC,UN,MF,PT3,PART35Z0)
	CALL SPART35LT(SC,TC,UC,Z3,PART35L)
	CALL SPART35LT(SC,TC,UC,UN,PART35L0)
c
	CALL VEC_DMULT_VECTOR(PART35Z,SF,K0MAX,TEMP0)
	CALL VEC_DMULT_VECTOR(PART35Z0,SF0,K0MAX,TEMP1)
	TY1 = 1.D0/Z3
	CALL VEC_DMULT_SUB(TEMP1,TEMP0,K0MAX,TY1,TEMP2)
	TY2 = 1.D0/(1.D0-Z3)
	CALL VEC_DMULT_CONSTANT(TEMP2,K0MAX,TY2,TEMP3)
C
	CALL VEC_DMULT_VECTOR(PART35L,SF,K0MAX,TEMP4)
	CALL VEC_DMULT_VECTOR(PART35L0,SF0,K0MAX,TEMP5)
	CALL VEC_DMULT_SUB(TEMP5,TEMP4,K0MAX,TY1,TEMP6)
	TY3 = DLOG(1.D0-Z3)/(1.D0-Z3)
	CALL VEC_DMULT_CONSTANT(TEMP6,K0MAX,TY3,TEMP7)
C
	TY4 = 1.D0/X3
	CALL VEC_DADD_MULT_CONSTANT(TEMP3,TEMP7,K0MAX,TY4,SY3P)
	RETURN
	END
C**********************************************************************
	SUBROUTINE SSY3PTP(S,GPT3,GPT4,Y3,Y4,MF,X3,X4,Z3,IH1,
     #	IH2,IH3,IH4,SY3PP)
	IMPLICIT REAL*8 (A-H,L-Z)
	COMMON/FLAGEXP/ICOUP_EXP
	COMMON/COUPEXP/PTM_EXP,R_EXP
	PARAMETER (K0MAX=84)
	DIMENSION PART35Z(K0MAX),PART35L(K0MAX)
	DIMENSION PART35Z0(K0MAX),PART35L0(K0MAX)
	DIMENSION SF(K0MAX),SF0(K0MAX)
	DIMENSION SY3PP(K0MAX)
	DIMENSION TEMP0(K0MAX),TEMP1(K0MAX),TEMP2(K0MAX),TEMP3(K0MAX)
	DIMENSION TEMP4(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	YS = (Y3-Y4)/2.D0
	Y = (Y3+Y4)/2.D0
	PT3 = GPT3/X3
	X10 = PT3*(DEXP(Y3)+DEXP(Y4))/DSQRT(S)
	X20 = PT3*(DEXP(-Y3)+DEXP(-Y4))/DSQRT(S)
	X40 = GPT4/GPT3*X3
	X1P = X10/Z3
	X2P = X20/Z3
c attention les elements de matrice 2 -> 2 sont independant de 
c l'echelle d'energie
	SC = X10*X20*S
	TC = -X10*DSQRT(S)*PT3*DEXP(-Y3)
	UC = -X20*DSQRT(S)*PT3*DEXP(Y3)
	CALL STRFRAT(X1P,IH1,X2P,IH2,X3,IH3,X4,IH4,SF)
	CALL SPART35ZT(SC,TC,UC,Z3,MF,PT3,PART35Z)
	CALL SPART35LT(SC,TC,UC,Z3,PART35L)
	CALL VEC_DMULT_VECTOR(PART35Z,SF,K0MAX,TEMP0)
	TY1 = 1.D0/Z3
	TY2 = 1.D0/(1.D0-Z3)*TY1
	CALL VEC_DMULT_CONSTANT(TEMP0,K0MAX,TY2,TEMP1)
C
	CALL VEC_DMULT_VECTOR(PART35L,SF,K0MAX,TEMP2)
	TY3 = DLOG(1.D0-Z3)/(1.D0-Z3)*TY1
	CALL VEC_DMULT_CONSTANT(TEMP2,K0MAX,TY3,TEMP3)
C
	TY4 = 1.D0/X3
	CALL VEC_DADD_MULT_CONSTANT(TEMP1,TEMP3,K0MAX,TY4,SY3PP)
	RETURN
	END
C**********************************************************************
	SUBROUTINE SSY4PT(S,GPT3,GPT4,Y3,Y4,MF,X3,X4,Z4,IH1,
     #	IH2,IH3,IH4,SY4P)
	IMPLICIT REAL*8 (A-H,L-Z)
	COMMON/FLAGEXP/ICOUP_EXP
	COMMON/COUPEXP/PTM_EXP,R_EXP
	PARAMETER (K0MAX=84)
	DIMENSION PART45Z(K0MAX),PART45L(K0MAX)
	DIMENSION PART45Z0(K0MAX),PART45L0(K0MAX)
	DIMENSION SF(K0MAX),SF0(K0MAX)
	DIMENSION SY4P(K0MAX)
	DIMENSION TEMP0(K0MAX),TEMP1(K0MAX),TEMP2(K0MAX),TEMP3(K0MAX)
	DIMENSION TEMP4(K0MAX),TEMP5(K0MAX),TEMP6(K0MAX),TEMP7(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	YS = (Y3-Y4)/2.D0
	Y = (Y3+Y4)/2.D0
	PT4 = GPT4/X4
	X10 = PT4*(DEXP(Y3)+DEXP(Y4))/DSQRT(S)
	X20 = PT4*(DEXP(-Y3)+DEXP(-Y4))/DSQRT(S)
	X30 = GPT3/GPT4*X4
	X1S = X10/Z4
	X2S = X20/Z4
c attention les elements de matrice 2 -> 2 sont independant de 
c l'echelle d'energie
	SC = X10*X20*S
	TC = -X20*DSQRT(S)*PT4*DEXP(Y4)
	UC = -X10*DSQRT(S)*PT4*DEXP(-Y4)
	CALL STRFRAT(X1S,IH1,X2S,IH2,X3,IH3,X4,IH4,SF)
	CALL STRFRAT(X10,IH1,X20,IH2,X30,IH3,X4,IH4,SF0)
	CALL SPART45ZT(SC,TC,UC,Z4,MF,PT4,PART45Z)
	CALL SPART45ZT(SC,TC,UC,UN,MF,PT4,PART45Z0)
	CALL SPART45LT(SC,TC,UC,Z4,PART45L)
	CALL SPART45LT(SC,TC,UC,UN,PART45L0)
c
	CALL VEC_DMULT_VECTOR(PART45Z,SF,K0MAX,TEMP0)
	CALL VEC_DMULT_VECTOR(PART45Z0,SF0,K0MAX,TEMP1)
	TY1 = 1.D0/Z4
	CALL VEC_DMULT_SUB(TEMP1,TEMP0,K0MAX,TY1,TEMP2)
	TY2 = 1.D0/(1.D0-Z4)
	CALL VEC_DMULT_CONSTANT(TEMP2,K0MAX,TY2,TEMP3)
C
	CALL VEC_DMULT_VECTOR(PART45L,SF,K0MAX,TEMP4)
	CALL VEC_DMULT_VECTOR(PART45L0,SF0,K0MAX,TEMP5)
	CALL VEC_DMULT_SUB(TEMP5,TEMP4,K0MAX,TY1,TEMP6)
	TY3 = DLOG(1.D0-Z4)/(1.D0-Z4)
	CALL VEC_DMULT_CONSTANT(TEMP6,K0MAX,TY3,TEMP7)
C
	TY4 = 1.D0/X4
	CALL VEC_DADD_MULT_CONSTANT(TEMP3,TEMP7,K0MAX,TY4,SY4P)
	RETURN
	END
C**********************************************************************
	SUBROUTINE SSY4PTP(S,GPT3,GPT4,Y3,Y4,MF,X3,X4,Z4,IH1,
     #	IH2,IH3,IH4,SY4PP)
	IMPLICIT REAL*8 (A-H,L-Z)
	COMMON/FLAGEXP/ICOUP_EXP
	COMMON/COUPEXP/PTM_EXP,R_EXP
	PARAMETER (K0MAX=84)
	DIMENSION PART45Z(K0MAX),PART45L(K0MAX)
	DIMENSION PART45Z0(K0MAX),PART45L0(K0MAX)
	DIMENSION SF(K0MAX),SF0(K0MAX)
	DIMENSION SY4PP(K0MAX)
	DIMENSION TEMP0(K0MAX),TEMP1(K0MAX),TEMP2(K0MAX),TEMP3(K0MAX)
	DIMENSION TEMP4(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	YS = (Y3-Y4)/2.D0
	Y = (Y3+Y4)/2.D0
	PT4 = GPT4/X4
	X10 = PT4*(DEXP(Y3)+DEXP(Y4))/DSQRT(S)
	X20 = PT4*(DEXP(-Y3)+DEXP(-Y4))/DSQRT(S)
	X30 = GPT3/GPT4*X4
	X1S = X10/Z4
	X2S = X20/Z4
c attention les elements de matrice 2 -> 2 sont independant de 
c l'echelle d'energie
	SC = X10*X20*S
	TC = -X20*DSQRT(S)*PT4*DEXP(Y4)
	UC = -X10*DSQRT(S)*PT4*DEXP(-Y4)
	CALL STRFRAT(X1S,IH1,X2S,IH2,X3,IH3,X4,IH4,SF)
	CALL SPART45ZT(SC,TC,UC,Z4,MF,PT4,PART45Z)
	CALL SPART45LT(SC,TC,UC,Z4,PART45L)
	CALL VEC_DMULT_VECTOR(PART45Z,SF,K0MAX,TEMP0)
	TY1 = 1.D0/Z4
	TY2 = 1.D0/(1.D0-Z4)*TY1
	CALL VEC_DMULT_CONSTANT(TEMP0,K0MAX,TY2,TEMP1)
C
	CALL VEC_DMULT_VECTOR(PART45L,SF,K0MAX,TEMP2)
	TY3 = DLOG(1.D0-Z4)/(1.D0-Z4)*TY1
	CALL VEC_DMULT_CONSTANT(TEMP2,K0MAX,TY3,TEMP3)
C
	TY4 = 1.D0/X4
	CALL VEC_DADD_MULT_CONSTANT(TEMP1,TEMP3,K0MAX,TY4,SY4PP)
	RETURN
	END
C*****************************************************
C TERME A L'ORDRE DE BORN	
C*****************************************************	
	SUBROUTINE SFBORNT(S,N,GPT3,Y3,Y4,X3,X4,IH1,IH2,IH3,IH4,FBORN)
	IMPLICIT REAL*8 (A-H,L-Z)
	PARAMETER (K0MAX=84)
	DIMENSION SF1(K0MAX),RBORN1(K0MAX)
	DIMENSION FBORN(K0MAX),TEMP0(K0MAX)
	ZERO = 0.D0
	UN = 1.D0
	YS = (Y3-Y4)/2.D0
	Y = (Y3+Y4)/2.D0
	PT = GPT3/X3
	X10 = 2.D0*PT/DSQRT(S)*DEXP(Y)*DCOSH(YS)	
	X20 = 2.D0*PT/DSQRT(S)*DEXP(-Y)*DCOSH(YS)
	if (x10.gt.un.or.x20.gt.un) then
	  CALL VEC_DINIT(FBORN,K0MAX,ZERO)
	  return
	endif
	SC = 4.D0*PT**2*DCOSH(YS)**2
	TC = -2.D0*PT**2*DCOSH(YS)*DEXP(-YS)
	UC = -2.D0*PT**2*DCOSH(YS)*DEXP(YS)
	CALL STRFRAT(X10,IH1,X20,IH2,X3,IH3,X4,IH4,SF1)
	CALL BORNT(SC,TC,UC,N,RBORN1)
	CALL VEC_DMULT_VECTOR(RBORN1,SF1,K0MAX,TEMP0)
	TY3 = 1.D0/X3
	CALL VEC_DMULT_CONSTANT(TEMP0,K0MAX,TY3,FBORN)
	RETURN
	END 
C	
